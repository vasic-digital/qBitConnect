plugins {

    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {

    namespace 'com.shareconnect.qbitconnect'
    compileSdk 36

    defaultConfig {

        applicationId 'com.shareconnect.qbitconnect'
        minSdk 28  // Required for SecurityAccess library compatibility
        targetSdk 36
        versionCode 1
        versionName '1.0.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {

        release {

            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {

            minifyEnabled false
            shrinkResources false
            applicationIdSuffix '.debug'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ['-opt-in=kotlin.time.ExperimentalTime', '-opt-in=kotlin.ExperimentalStdlibApi']
    }

    buildFeatures {

        compose true
    }

    packaging {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/INDEX.LIST']
        }
    }

    lint {
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    testOptions {
        unitTests.all {
            it.jvmArgs '-XX:+AllowRedefinitionToAddDeleteMethods'
        }
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

tasks.withType(Test) {
    useJUnit()
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(17))
    })
}

dependencies {
    implementation project(':preferences')
    implementation project(':shared')
    implementation project(':DesignSystem')
    implementation project(':Onboarding')
    implementation project(':Asinka:asinka')
    implementation project(':ThemeSync')
    implementation project(':ProfileSync')
    implementation project(':HistorySync')
    implementation project(':RSSSync')
    implementation project(':BookmarkSync')
    implementation project(':PreferencesSync')
    implementation project(':LanguageSync')
    implementation project(':TorrentSharingSync')
    implementation project(':Localizations')
    implementation project(':Toolkit:SecurityAccess')

    def androidxLifecycleVersion = '2.9.4'
    def composeNavigationVersion = '2.9.0-beta03'
    def coroutinesVersion = '1.10.2'
    def kotlinxSerializationVersion = '1.7.3'
    def kotlinxDatetimeVersion = '0.6.1'

    def ktorVersion = '3.2.3'
    def okhttpVersion = '4.12.0'
    def coilVersion = '2.7.0'
    def htmlConverterVersion = '1.1.0'
    def multiplatformSettingsVersion = '1.3.0'
    def materialKolorVersion = '2.1.1'
    def fileKitVersion = '0.8.2'
    def reorderableVersion = '2.5.1'
    def autolinktextVersion = '2.0.2'
    def composePipetteVersion = '1.0.1'
    def androidxAppcompatVersion = '1.7.1'
    def materialVersion = '1.13.0'
    def profileinstallerVersion = '1.4.1'
    def accompanistVersion = '0.37.3'
    def workRuntimeVersion = '2.10.4'

    implementation platform('androidx.compose:compose-bom:2025.09.00')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    implementation 'androidx.compose.runtime:runtime'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation "org.jetbrains.androidx.lifecycle:lifecycle-viewmodel:$androidxLifecycleVersion"
    implementation "org.jetbrains.androidx.lifecycle:lifecycle-runtime-compose:$androidxLifecycleVersion"
    implementation 'androidx.compose.material:material-icons-extended'

    implementation 'androidx.activity:activity-compose:1.11.0'
    implementation 'androidx.compose.material3:material3-window-size-class'

    implementation "org.jetbrains.androidx.navigation:navigation-compose:$composeNavigationVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinxSerializationVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:$kotlinxDatetimeVersion"

    implementation "io.ktor:ktor-client-core:$ktorVersion"
    implementation "io.ktor:ktor-client-okhttp:$ktorVersion"
    implementation "io.ktor:ktor-client-content-negotiation:$ktorVersion"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktorVersion"
    implementation "io.ktor:ktor-client-auth:$ktorVersion"

    implementation project.dependencies.platform("com.squareup.okhttp3:okhttp-bom:$okhttpVersion")
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.squareup.okhttp3:logging-interceptor'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps'

    implementation "io.coil-kt:coil-compose:$coilVersion"
    implementation "io.coil-kt:coil-svg:$coilVersion"

    implementation "be.digitalia.compose.htmlconverter:htmlconverter:$htmlConverterVersion"

    implementation "com.russhwolf:multiplatform-settings:$multiplatformSettingsVersion"

    implementation "com.materialkolor:material-kolor:$materialKolorVersion"

    // FileKit temporarily commented out due to version compatibility issues
    // implementation "io.github.vinceglb:filekit-core:$fileKitVersion"
    // implementation "io.github.vinceglb:filekit-dialogs:$fileKitVersion"

    implementation "sh.calvin.reorderable:reorderable:$reorderableVersion"

    implementation "sh.calvin.autolinktext:autolinktext:$autolinktextVersion"

    implementation "dev.zt64.compose.pipette:compose-pipette:$composePipetteVersion"

    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"

    implementation "com.google.android.material:material:$materialVersion"

    implementation "androidx.profileinstaller:profileinstaller:$profileinstallerVersion"

    implementation "com.google.accompanist:accompanist-permissions:$accompanistVersion"

    implementation "androidx.work:work-runtime-ktx:$workRuntimeVersion"

    implementation 'androidx.datastore:datastore-preferences:1.1.1'

    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation 'androidx.datastore:datastore-preferences-core:1.1.1'
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation "com.russhwolf:multiplatform-settings-test:$multiplatformSettingsVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:2.0.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:2.0.0"

    // Android test dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.7.7'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.7.7'

    // Additional test dependencies for androidTest
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
}

// Force Kotlin version to avoid conflicts
configurations.all {
    resolutionStrategy {
        force 'org.jetbrains.kotlin:kotlin-stdlib:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-reflect:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-common:2.0.0'
    }
}