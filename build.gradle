import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask

plugins {
    id 'org.jmailen.kotlinter' version '5.2.0' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.2.20' apply false
    id 'com.github.gmazzo.buildconfig' version '5.6.7' apply false
    id 'com.github.ben-manes.versions' version '0.52.0'
}

tasks.withType(DependencyUpdatesTask) {
    gradleReleaseChannel = 'current'

    rejectVersionIf {
        def isStableVersion = { String version ->
            def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.contains(it) }
            def regex = ~/^[0-9,.v-]+(-r)?$/
            return stableKeyword || regex.matcher(version).matches()
        }

        isStableVersion(candidate.version) != isStableVersion(currentVersion)
    }
}

tasks.register('clean', Delete) {
    delete layout.buildDirectory
}